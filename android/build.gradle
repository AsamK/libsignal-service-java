buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "signal-service-android"
version          = version_number
group            = group_info

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.whispersystems:signal-protocol-android:${signal_version}"
    compile (project(':java')) {
        exclude group: 'org.whispersystems', module: 'signal-protocol-java'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
    lintOptions {
        warning 'InvalidPackage'
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


def ossrhUsername = project.properties['ossrhUsername'] ?: ""
def ossrhPassword = project.properties['ossrhPassword'] ?: ""

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
            name 'signal-service-android'
            packaging 'aar'
            description 'Signal Service communication library for Android, unofficial fork'
            url 'https://github.com/Turasa/libsignal-service-java'

            scm {
                url 'scm:git@github.com:Turasa/libsignal-service-java.git'
                connection 'scm:git@github.com:Turasa/libsignal-service-java.git'
                developerConnection 'scm:git@github.com:Turasa/libsignal-service-java.git'
            }

            licenses {
                license {
                    name 'GPLv3'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Moxie Marlinspike'
                }
                developer {
                    name 'Sebastian Scheibner'
                }
                developer {
                    name 'Tilman Hoffbauer'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}
